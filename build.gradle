plugins {
    id 'org.springframework.boot' version '2.7.1'
    id 'io.spring.dependency-management' version '1.0.12.RELEASE'
    id 'io.freefair.lombok' version '6.5.0.3'
}

subprojects {
    group = 'com.sha' // our domain group
    version = '1.0' // we don't use versioning so it can be any semver

    repositories {
        mavenCentral()
    }

    apply plugin: 'java' // just java plugin
    apply plugin: 'java-library' // for api() to work in gradle files which replaces compile()
    apply plugin: 'io.freefair.lombok'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management' // auto dependency resolutions for boot

    sourceCompatibility = '17'
    targetCompatibility = '17'

    dependencies {
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testRuntimeOnly('com.h2database:h2')
    }

    test {
        useJUnitPlatform()
        jvmArgs '--add-opens', 'java.base/java.time=ALL-UNNAMED'    // required for gson Java 17 compatibility. this should be removed in Spring Boot 3+
    }

    tasks.withType(JavaCompile) { // generic encoding to prevent some problems
        options.encoding = 'UTF-8'
    }
}
